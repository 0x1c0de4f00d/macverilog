AC_INIT(Makefile.in)

AC_PROG_CC
AC_PROG_CXX
AC_CHECK_TOOL(STRIP, strip, true)

AC_CYGWIN
AC_EXEEXT
AC_MINGW32
AC_SUBST(EXEEXT)

AC_PROG_INSTALL

AC_CHECK_HEADERS(malloc.h)

AC_CHECK_SIZEOF(unsigned long long)
AC_CHECK_SIZEOF(unsigned long)
AC_CHECK_SIZEOF(unsigned)

# --
# Look for a dl library to use. First look for the standard dlopen
# functions, and failing that look for the HP specific shl_load function.

AC_CHECK_HEADERS(dlfcn.h dl.h, break)

DLLIB=''
AC_CHECK_LIB(dl,dlopen,[DLLIB=-ldl])
if test -z "$DLLIB" ; then
AC_CHECK_LIB(dld,shl_load,[DLLIB=-ldld])
fi
AC_SUBST(DLLIB)


# Darwin requires -no-cpp-precomp
case "${host}" in
    *-*-darwin*)
	CPPFLAGS="-no-cpp-precomp $CPPFLAGS"
	CFLAGS="-no-cpp-precomp $CFLAGS"
	;;
esac

# The -fPIC flag is used to tell the compiler to make position
# independent code. It is needed when making shared objects.

AC_MSG_CHECKING("for flag to make position independent code")
PICFLAG=-fPIC
case "${host}" in

     *-*-cygwin*)
	PICFLAG=
	;;

     *-*-hpux*)
	PICFLAG=+z
	;;

esac
AC_SUBST(PICFLAG)
AC_MSG_RESULT($PICFLAG)

# The -rdynamic flag is used by iverilog when compiling the target,
# to know how to export symbols of the main program to loadable modules
# that are brought in by -ldl. VPI support requires this.
AC_MSG_CHECKING("for -rdynamic compiler flag")

rdynamic=-rdynamic
whole_archive=-Wl,-whole-archive
no_whole_archive=-Wl,-no-whole-archive
case "${host}" in

    *-*-netbsd*)
	rdynamic="-Wl,--export-dynamic"
	;;

    *-*-openbsd*)
	whole_archive=""
	no_whole_archive=""
	;;

    *-*-solaris*)
	rdynamic=""
	;;

    *-*-cygwin*)
	rdynamic=""
	;;

    *-*-hpux*)
	rdynamic="-E"
	;;

    *-*-darwin*)
	rdynamic="-Wl,-all_load"
	whole_archive=""
	no_whole_archive=""
	;;

esac
AC_SUBST(rdynamic)
AC_SUBST(whole_archive)
AC_SUBST(no_whole_archive)
AC_MSG_RESULT($rdynamic)

AC_MSG_CHECKING("for -whole-archive compiler flags")
AC_MSG_RESULT($whole_archive $no_whole_archive)

AC_MSG_CHECKING("for shared library link flag")
shared=-shared
case "${host}" in

     *-*-cygwin*)
        shared="-shared -Wl,--enable-auto-image-base"
	;;

     *-*-hpux*)
	shared="-b"
	;;

     *-*-darwin1.[0123])
	shared="-bundle -undefined suppress"
	;;

     *-*-darwin*)
	shared="-bundle -undefined suppress -flat_namespace"
	;;
esac

AC_SUBST(shared)
AC_MSG_RESULT($shared)

AC_MSG_CHECKING("for extra libs needed")
EXTRALIBS=
case "${host}" in
     *-*-cygwin*)
	EXTRALIBS="-liberty"
	;;
esac
AC_SUBST(EXTRALIBS)
AC_MSG_RESULT($EXTRALIBS)

#######################
## test for underscores. The vpi module loader in vvm needs to know this
## in order to know the name of the start symbol for the .vpi module.
#######################

AC_CYGWIN
AC_EXEEXT
AC_MINGW32


WIN32=no
AC_MSG_CHECKING("Checking for windows")
if test "$CYGWIN" = "yes" -o "$MINGW32" = "yes"
then
    WIN32=yes
fi
AC_SUBST(WIN32)
AC_MSG_RESULT($WIN32)
AC_SUBST(EXEEXT)


AC_MSG_CHECKING("for leading and/or trailing underscores")
cat << EOF > underscore.c
       void underscore(void){}
EOF
$CC -shared -c underscore.c > /dev/null 2>&1

CC_LEADING_UNDERSCORE=no
CC_TRAILING_UNDERSCORE=no

output=`nm underscore.o|grep _underscore 2>&1`
if test ! -z "$output" -a "$CYGWIN" != "yes" -a "$MINGW32" != "yes"; then
  CC_LEADING_UNDERSCORE=yes
  AC_DEFINE(NEED_LU)
fi



output=`nm underscore.o|grep underscore_ 2>&1`
if test ! -z "$output"; then
  CC_TRAILING_UNDERSCORE=yes
  AC_DEFINE(NEED_TU)
fi

if test "$CC_LEADING_UNDERSCORE" = yes; then
  AC_DEFINE(WLU)
fi
if test "$CC_TRAILING_UNDERSCORE" = yes; then
  AC_DEFINE(WTU)
fi

rm underscore.c underscore.o

AC_MSG_RESULT("$CC_LEADING_UNDERSCORE $CC_TRAILING_UNDERSCORE")

#######################
## end of test for underscores
#######################



AC_MSG_CHECKING("for ident support in C compiler")
ident_support='-DHAVE_CVS_IDENT=1'
case "${host}" in

     *-*-cygwin*)
	ident_support=
	;;

     *-*-darwin*)
	ident_support=
	;;

     *-*-machten*)
	ident_support=
	;;
esac

AC_SUBST(ident_support)
AC_MSG_RESULT($ident_support)

AC_OUTPUT(Makefile)
